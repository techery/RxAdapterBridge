buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.ben-manes.versions'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  dexOptions {
    preDexLibraries = rootProject.ext.preDexLibs
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_6
    targetCompatibility JavaVersion.VERSION_1_6
  }
}

repositories {
  jcenter()
}

dependencies {
  compile 'com.android.support:recyclerview-v7:24.2.1'
  //
  compile 'io.reactivex:rxjava:1.2.1'
  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'com.trello:rxlifecycle:0.8.0'
  compile 'com.trello:rxlifecycle-android:0.8.0'
  //
  testCompile 'junit:junit:4.12'
  //
  androidTestCompile('com.android.support.test:runner:0.5')
  androidTestCompile('com.android.support.test:rules:0.5')
  androidTestCompile 'com.android.support:support-annotations:24.2.1'
  //
  androidTestCompile 'org.mockito:mockito-core:2.0.10-beta'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
  androidTestCompile 'com.artfulbits:meter:1.0.1.164'
  androidTestCompile 'net.jodah:concurrentunit:0.4.2'
  androidTestCompile 'com.jakewharton.timber:timber:4.1.2'
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

  options.memberLevel = JavadocMemberLevel.PROTECTED
  options.links("http://docs.oracle.com/javase/7/docs/api/")
  options.links("http://developer.android.com/reference/packages.html")
  exclude '**/BuildConfig.java'
  exclude '**/R.java'
  exclude '**/internal/**'
  failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
